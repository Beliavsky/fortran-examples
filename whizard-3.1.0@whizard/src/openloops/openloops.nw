% -*- ess-noweb-default-code-mode: f90-mode; noweb-default-code-mode: f90-mode; -*-
% WHIZARD code as NOWEB source: interface to OpenLoops 1-loop library
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\chapter{OpenLoops Interface}
\includemodulegraph{openloops}

The interface to OpenLoops.

\clearpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
<<[[prc_openloops.f90]]>>=
<<File header>>

module prc_openloops

  use, intrinsic :: iso_c_binding !NODEP!

  use kinds
<<Use strings>>
  use string_utils, only: str
  use physics_defs
  use variables
  use os_interface
  use lorentz
  use interactions
  use model_data
  use prc_core_def
  use prc_core

  use blha_config
  use blha_olp_interfaces

<<Use mpi f08>>

<<Standard module head>>

<<Prc openloops: public>>

<<Prc openloops: main parameters>>

<<Prc openloops: types>>

<<Prc openloops: interfaces>>

  interface
<<Prc openloops: sub interfaces>>
  end interface

contains

<<Prc openloops: main procedures>>

end module prc_openloops
@ %def module prc_openloops
@
<<[[prc_openloops_sub.f90]]>>=
<<File header>>

submodule (prc_openloops) prc_openloops_s

<<Use debug>>
  use io_units
  use constants
  use numeric_utils
  use diagnostics
  use system_dependencies
  use sm_physics, only: top_width_sm_lo, top_width_sm_qcd_nlo_jk
  use sm_qcd
  use prclib_interfaces

  implicit none

<<Prc openloops: parameters>>

contains

<<Prc openloops: procedures>>

end submodule prc_openloops_s

@ %def prc_openloops_s
@
<<Prc openloops: parameters>>=
  real(default), parameter :: openloops_default_bmass = 0._default
  real(default), parameter :: openloops_default_topmass = 172._default
  real(default), parameter :: openloops_default_topwidth = 0._default
  real(default), parameter :: openloops_default_wmass = 80.399_default
  real(default), parameter :: openloops_default_wwidth = 0._default
  real(default), parameter :: openloops_default_zmass = 91.1876_default
  real(default), parameter :: openloops_default_zwidth = 0._default
  real(default), parameter :: openloops_default_higgsmass = 125._default
  real(default), parameter :: openloops_default_higgswidth = 0._default

<<Prc openloops: main parameters>>=
  integer :: N_EXTERNAL = 0

@ %def openloops default parameter
@
<<Prc openloops: interfaces>>=
  abstract interface
     subroutine ol_evaluate_scpowheg (id, pp, emitter, res, resmunu) bind(C)
       import
       integer(kind = c_int), value :: id, emitter
       real(kind = c_double), intent(in) :: pp(5 * N_EXTERNAL)
       real(kind = c_double), intent(out) :: res, resmunu(16)
     end subroutine ol_evaluate_scpowheg
  end interface

@ %def ol_evaluate_scpowheg interface
@
<<Prc openloops: interfaces>>=
  abstract interface
     subroutine ol_getparameter_double (variable_name, value) bind(C)
       import
       character(kind = c_char,len = 1), intent(in) :: variable_name
       real(kind = c_double), intent(out) :: value
     end subroutine ol_getparameter_double
  end interface

@ %def ol_getparameter_double interface
@
<<Prc openloops: types>>=
  type, extends (prc_blha_writer_t) :: openloops_writer_t
  contains
  <<Prc openloops: openloops writer: TBP>>
  end type openloops_writer_t

@ %def openloops_writer_t
@
<<Prc openloops: public>>=
  public :: openloops_def_t
<<Prc openloops: types>>=
  type, extends (blha_def_t) :: openloops_def_t
     integer :: verbosity
  contains
  <<Prc openloops: openloops def: TBP>>
  end type openloops_def_t

@ %def openloops_def_t
@
<<Prc openloops: types>>=
  type, extends (blha_driver_t) :: openloops_driver_t
    integer :: n_external = 0
    type(string_t) :: olp_file
    procedure(ol_evaluate_scpowheg), nopass, pointer :: &
         evaluate_spin_correlations_powheg => null ()
    procedure(ol_getparameter_double), nopass, pointer :: &
         get_parameter_double => null ()
  contains
  <<Prc openloops: openloops driver: TBP>>
  end type openloops_driver_t

@ %def openloops_driver_t
@
<<Prc openloops: types>>=
  type :: openloops_threshold_data_t
    logical :: nlo = .true.
    real(default) :: alpha_ew
    real(default) :: sinthw
    real(default) :: m_b, m_W
    real(default) :: vtb
  contains
  <<Prc openloops: openloops threshold data: TBP>>
  end type openloops_threshold_data_t

@ %def openloops_threshold_data_t
@
<<Prc openloops: openloops threshold data: TBP>>=
  procedure :: compute_top_width => &
       openloops_threshold_data_compute_top_width
<<Prc openloops: sub interfaces>>=
    module function openloops_threshold_data_compute_top_width &
         (data, mtop, alpha_s) result (wtop)
      real(default) :: wtop
      class(openloops_threshold_data_t), intent(in) :: data
      real(default), intent(in) :: mtop, alpha_s
    end function openloops_threshold_data_compute_top_width
<<Prc openloops: procedures>>=
  module function openloops_threshold_data_compute_top_width &
       (data, mtop, alpha_s) result (wtop)
    real(default) :: wtop
    class(openloops_threshold_data_t), intent(in) :: data
    real(default), intent(in) :: mtop, alpha_s
    if (data%nlo) then
       wtop = top_width_sm_qcd_nlo_jk (data%alpha_ew, data%sinthw, &
              data%vtb, mtop, data%m_W, data%m_b, alpha_s)
    else
       wtop = top_width_sm_lo (data%alpha_ew, data%sinthw, data%vtb, &
              mtop, data%m_W, data%m_b)
    end if
  end function openloops_threshold_data_compute_top_width

@ %def openloops_threshold_data_compute_top_width
@
<<Prc openloops: public>>=
  public :: openloops_state_t
<<Prc openloops: types>>=
  type, extends (blha_state_t) :: openloops_state_t
    type(openloops_threshold_data_t), allocatable :: threshold_data
  contains
  <<Prc openloops: openloops state: TBP>>
  end type openloops_state_t

@ %def openloops_state_t
@
<<Prc openloops: openloops state: TBP>>=
  procedure :: init_threshold => openloops_state_init_threshold
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_state_init_threshold (object, model)
      class(openloops_state_t), intent(inout) :: object
      type(model_data_t), intent(in) :: model
    end subroutine openloops_state_init_threshold
<<Prc openloops: procedures>>=
  module subroutine openloops_state_init_threshold (object, model)
    class(openloops_state_t), intent(inout) :: object
    type(model_data_t), intent(in) :: model
    if (model%get_name () == "SM_tt_threshold") then
       allocate (object%threshold_data)
       associate (data => object%threshold_data)
          data%nlo = btest (int (model%get_real (var_str ('offshell_strategy'))), 0)
          data%alpha_ew = one / model%get_real (var_str ('alpha_em_i'))
          data%sinthw = model%get_real (var_str ('sw'))
          data%m_b = model%get_real (var_str ('mb'))
          data%m_W = model%get_real (var_str ('mW'))
          data%vtb = model%get_real (var_str ('Vtb'))
       end associate
    end if
  end subroutine openloops_state_init_threshold

@ %def openloops_state_init_threshold
@
<<Prc openloops: public>>=
  public :: prc_openloops_t
<<Prc openloops: types>>=
  type, extends (prc_blha_t) :: prc_openloops_t
  contains
  <<Prc openloops: prc openloops: TBP>>
  end type prc_openloops_t

@ %def prc_openloops_t
@
<<Prc openloops: openloops writer: TBP>>=
  procedure, nopass :: type_name => openloops_writer_type_name
<<Prc openloops: sub interfaces>>=
    module function openloops_writer_type_name () result (string)
      type(string_t) :: string
    end function openloops_writer_type_name
<<Prc openloops: procedures>>=
  module function openloops_writer_type_name () result (string)
    type(string_t) :: string
    string = "openloops"
  end function openloops_writer_type_name

@ %def openloops_writer_type_name
@ Gfortran 7/8/9 bug: has to remain in the main module.
<<Prc openloops: openloops def: TBP>>=
  procedure :: init => openloops_def_init
<<Prc openloops: main procedures>>=
  subroutine openloops_def_init (object, basename, model_name, &
     prt_in, prt_out, nlo_type, restrictions, var_list)
    class(openloops_def_t), intent(inout) :: object
    type(string_t), intent(in) :: basename, model_name
    type(string_t), dimension(:), intent(in) :: prt_in, prt_out
    integer, intent(in) :: nlo_type
    type(string_t), intent(in), optional :: restrictions
    type(var_list_t), intent(in) :: var_list
  <<Prc openloops: openloops def init: variables>>
    object%basename = basename
    allocate (openloops_writer_t :: object%writer)
    select case (nlo_type)
    case (BORN)
       object%suffix = '_BORN'
    case (NLO_REAL)
       object%suffix = '_REAL'
    case (NLO_VIRTUAL)
       object%suffix = '_LOOP'
    case (NLO_SUBTRACTION, NLO_MISMATCH)
       object%suffix = '_SUB'
    case (NLO_DGLAP)
       object%suffix = '_DGLAP'
    end select
  <<Prc openloops: openloops def init: suffix>>
    select type (writer => object%writer)
    class is (prc_blha_writer_t)
       call writer%init (model_name, prt_in, prt_out, restrictions)
    end select
    object%verbosity = var_list%get_ival (var_str ("openloops_verbosity"))
  end subroutine openloops_def_init

@ %def openloops_def_init
@ Add additional suffix for each rank of the communicator, such that the
filenames do not clash.
<<MPI: Prc openloops: openloops def init: variables>>=
  integer :: n_size, rank
<<MPI: Prc openloops: openloops def init: suffix>>=
  call MPI_comm_rank (MPI_COMM_WORLD, rank)
  call MPI_Comm_size (MPI_COMM_WORLD, n_size)
  if (n_size > 1) then
     object%suffix = object%suffix // var_str ("_") // str (rank)
  end if
@
<<Prc openloops: openloops def: TBP>>=
  procedure, nopass :: type_string => openloops_def_type_string
<<Prc openloops: sub interfaces>>=
    module function openloops_def_type_string () result (string)
      type(string_t) :: string
    end function openloops_def_type_string
<<Prc openloops: procedures>>=
  module function openloops_def_type_string () result (string)
    type(string_t) :: string
    string = "openloops"
  end function openloops_def_type_string

@
@ %def openloops_def_type_string
<<Prc openloops: openloops def: TBP>>=
  procedure :: write => openloops_def_write
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_def_write (object, unit)
      class(openloops_def_t), intent(in) :: object
      integer, intent(in) :: unit
    end subroutine openloops_def_write
<<Prc openloops: procedures>>=
  module subroutine openloops_def_write (object, unit)
    class(openloops_def_t), intent(in) :: object
    integer, intent(in) :: unit
    select type (writer => object%writer)
    type is (openloops_writer_t)
       call writer%write (unit)
    end select
  end subroutine openloops_def_write

@
@ %def openloops_def_write
<<Prc openloops: openloops driver: TBP>>=
  procedure :: init_dlaccess_to_library => &
       openloops_driver_init_dlaccess_to_library
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_init_dlaccess_to_library &
       (object, os_data, dlaccess, success)
      class(openloops_driver_t), intent(in) :: object
      type(os_data_t), intent(in) :: os_data
      type(dlaccess_t), intent(out) :: dlaccess
      logical, intent(out) :: success
    end subroutine openloops_driver_init_dlaccess_to_library
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_init_dlaccess_to_library &
     (object, os_data, dlaccess, success)
    class(openloops_driver_t), intent(in) :: object
    type(os_data_t), intent(in) :: os_data
    type(dlaccess_t), intent(out) :: dlaccess
    logical, intent(out) :: success
    type(string_t) :: ol_library, msg_buffer
    ol_library = OPENLOOPS_DIR // '/lib/libopenloops.' // &
         os_data%shrlib_ext
    msg_buffer = "One-Loop-Provider: Using OpenLoops"
    call msg_message (char(msg_buffer))
    msg_buffer = "Loading library: " // ol_library
    call msg_message (char(msg_buffer))
    if (os_file_exist (ol_library)) then
       call dlaccess_init (dlaccess, var_str (""), ol_library, os_data)
    else
       call msg_fatal ("Link OpenLoops: library not found")
    end if
    success = .not. dlaccess_has_error (dlaccess)
  end subroutine openloops_driver_init_dlaccess_to_library

@ %def openloops_driver_init_dlaccess_to_library
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: set_alpha_s => openloops_driver_set_alpha_s
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_set_alpha_s (driver, alpha_s)
      class(openloops_driver_t), intent(in) :: driver
      real(default), intent(in) :: alpha_s
    end subroutine openloops_driver_set_alpha_s
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_set_alpha_s (driver, alpha_s)
    class(openloops_driver_t), intent(in) :: driver
    real(default), intent(in) :: alpha_s
    integer :: ierr
    if (associated (driver%blha_olp_set_parameter)) then
       call driver%blha_olp_set_parameter &
            (c_char_'alphas'//c_null_char, &
             dble (alpha_s), 0._double, ierr)
    else
       call msg_fatal ("blha_olp_set_parameter not associated!")
    end if
    if (ierr == 0) call parameter_error_message (var_str ('alphas'), &
         var_str ('openloops_driver_set_alpha_s'))
  end subroutine openloops_driver_set_alpha_s

@ %def openloops_driver_set_alpha_s
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: set_alpha_qed => openloops_driver_set_alpha_qed
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_set_alpha_qed (driver, alpha)
      class(openloops_driver_t), intent(inout) :: driver
      real(default), intent(in) :: alpha
    end subroutine openloops_driver_set_alpha_qed
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_set_alpha_qed (driver, alpha)
    class(openloops_driver_t), intent(inout) :: driver
    real(default), intent(in) :: alpha
    integer :: ierr
    call driver%blha_olp_set_parameter &
       (c_char_'alpha_qed'//c_null_char, &
        dble (alpha), 0._double, ierr)
    if (ierr == 0) call ew_parameter_error_message (var_str ('alpha_qed'))
  end subroutine openloops_driver_set_alpha_qed

@ %def openloops_driver_set_alpha_qed
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: set_GF => openloops_driver_set_GF
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_set_GF (driver, GF)
      class(openloops_driver_t), intent(inout) :: driver
      real(default), intent(in) :: GF
    end subroutine openloops_driver_set_GF
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_set_GF (driver, GF)
    class(openloops_driver_t), intent(inout) :: driver
    real(default), intent(in) :: GF
    integer :: ierr
    call driver%blha_olp_set_parameter &
       (c_char_'Gmu'//c_null_char, &
        dble(GF), 0._double, ierr)
    if (ierr == 0) call ew_parameter_error_message (var_str ('Gmu'))
  end subroutine openloops_driver_set_GF

@ %def openloops_driver_set_GF
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: set_weinberg_angle => openloops_driver_set_weinberg_angle
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_set_weinberg_angle (driver, sw2)
      class(openloops_driver_t), intent(inout) :: driver
      real(default), intent(in) :: sw2
    end subroutine openloops_driver_set_weinberg_angle
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_set_weinberg_angle (driver, sw2)
    class(openloops_driver_t), intent(inout) :: driver
    real(default), intent(in) :: sw2
    integer :: ierr
    call driver%blha_olp_set_parameter &
       (c_char_'sw2'//c_null_char, &
        dble(sw2), 0._double, ierr)
    if (ierr == 0) call ew_parameter_error_message (var_str ('sw2'))
  end subroutine openloops_driver_set_weinberg_angle

@ %def openloops_driver_set_weinberg_angle
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: print_alpha_s => openloops_driver_print_alpha_s
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_print_alpha_s (object)
      class(openloops_driver_t), intent(in) :: object
    end subroutine openloops_driver_print_alpha_s
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_print_alpha_s (object)
    class(openloops_driver_t), intent(in) :: object
    call object%blha_olp_print_parameter (c_char_'alphas'//c_null_char)
  end subroutine openloops_driver_print_alpha_s

@ %def openloops_driver_print_alpha_s
@
<<Prc openloops: openloops driver: TBP>>=
  procedure, nopass :: type_name => openloops_driver_type_name
<<Prc openloops: sub interfaces>>=
    module function openloops_driver_type_name () result (type)
      type(string_t) :: type
    end function openloops_driver_type_name
<<Prc openloops: procedures>>=
  module function openloops_driver_type_name () result (type)
    type(string_t) :: type
    type = "OpenLoops"
  end function openloops_driver_type_name

@ %def openloops_driver_type_name
@
<<Prc openloops: openloops driver: TBP>>=
  procedure :: load_procedures => openloops_driver_load_procedures
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_driver_load_procedures &
         (object, os_data, success)
      class(openloops_driver_t), intent(inout) :: object
      type(os_data_t), intent(in) :: os_data
      logical, intent(out) :: success
    end subroutine openloops_driver_load_procedures
<<Prc openloops: procedures>>=
  module subroutine openloops_driver_load_procedures &
       (object, os_data, success)
    class(openloops_driver_t), intent(inout) :: object
    type(os_data_t), intent(in) :: os_data
    logical, intent(out) :: success
    type(dlaccess_t) :: dlaccess
    type(c_funptr) :: c_fptr
    logical :: init_success

    call object%init_dlaccess_to_library (os_data, dlaccess, init_success)

    c_fptr = dlaccess_get_c_funptr (dlaccess, var_str ("ol_evaluate_scpowheg"))
    call c_f_procpointer (c_fptr, object%evaluate_spin_correlations_powheg)
    call check_for_error (var_str ("ol_evaluate_scpowheg"))
    c_fptr = dlaccess_get_c_funptr (dlaccess, var_str ("ol_getparameter_double"))
    call c_f_procpointer (c_fptr, object%get_parameter_double)
    call check_for_error (var_str ("ol_getparameter_double"))
    success = .true.

    contains
      subroutine check_for_error (function_name)
        type(string_t), intent(in) :: function_name
        if (dlaccess_has_error (dlaccess)) &
           call msg_fatal (char ("Loading of " // function_name // " failed!"))
     end subroutine check_for_error
  end subroutine openloops_driver_load_procedures

@ %def openloops_driver_load_procedures
@
<<Prc openloops: openloops def: TBP>>=
  procedure :: read => openloops_def_read
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_def_read (object, unit)
      class(openloops_def_t), intent(out) :: object
      integer, intent(in) :: unit
    end subroutine openloops_def_read
<<Prc openloops: procedures>>=
  module subroutine openloops_def_read (object, unit)
    class(openloops_def_t), intent(out) :: object
    integer, intent(in) :: unit
  end subroutine openloops_def_read

@ %def openloops_def_read
@ Gfortran 7/8/9 bug: has to remain in the main module.
<<Prc openloops: openloops def: TBP>>=
  procedure :: allocate_driver => openloops_def_allocate_driver
<<Prc openloops: main procedures>>=
  subroutine openloops_def_allocate_driver (object, driver, basename)
    class(openloops_def_t), intent(in) :: object
    class(prc_core_driver_t), intent(out), allocatable :: driver
    type(string_t), intent(in) :: basename
    if (.not. allocated (driver)) allocate (openloops_driver_t :: driver)
  end subroutine openloops_def_allocate_driver

@
@ %def openloops_def_allocate_driver
<<Prc openloops: openloops state: TBP>>=
  procedure :: write => openloops_state_write
<<Prc openloops: sub interfaces>>=
    module subroutine openloops_state_write (object, unit)
      class(openloops_state_t), intent(in) :: object
      integer, intent(in), optional :: unit
    end subroutine openloops_state_write
<<Prc openloops: procedures>>=
  module subroutine openloops_state_write (object, unit)
    class(openloops_state_t), intent(in) :: object
    integer, intent(in), optional :: unit
  end subroutine openloops_state_write

@ %def prc_openloops_state_write
@ Gfortran 7/8/9 bug: has to remain in the main module.
<<Prc openloops: prc openloops: TBP>>=
  procedure :: allocate_workspace => prc_openloops_allocate_workspace
<<Prc openloops: main procedures>>=
  subroutine prc_openloops_allocate_workspace (object, core_state)
    class(prc_openloops_t), intent(in) :: object
    class(prc_core_state_t), intent(inout), allocatable :: core_state
    allocate (openloops_state_t :: core_state)
  end subroutine prc_openloops_allocate_workspace

@ %def prc_openloops_allocate_workspace
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: init_driver => prc_openloops_init_driver
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_init_driver (object, os_data)
      class(prc_openloops_t), intent(inout) :: object
      type(os_data_t), intent(in) :: os_data
    end subroutine prc_openloops_init_driver
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_init_driver (object, os_data)
    class(prc_openloops_t), intent(inout) :: object
    type(os_data_t), intent(in) :: os_data
    type(string_t) :: olp_file, olc_file
    type(string_t) :: suffix

    select type (def => object%def)
    type is (openloops_def_t)
       suffix = def%suffix
       olp_file = def%basename // suffix // '.olp'
       olc_file = def%basename // suffix // '.olc'
    class default
       call msg_bug ("prc_openloops_init_driver: core_def should be openloops-type")
    end select

    select type (driver => object%driver)
    type is (openloops_driver_t)
       driver%olp_file = olp_file
       driver%contract_file = olc_file
       driver%nlo_suffix = suffix
    end select
  end subroutine prc_openloops_init_driver

@ %def prc_openloops_init_driver
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: write => prc_openloops_write
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_write (object, unit)
      class(prc_openloops_t), intent(in) :: object
      integer, intent(in), optional :: unit
    end subroutine prc_openloops_write
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_write (object, unit)
    class(prc_openloops_t), intent(in) :: object
    integer, intent(in), optional :: unit
    call msg_message (unit = unit, string = "OpenLoops")
  end subroutine prc_openloops_write

@
@ %def prc_openloops_write
<<Prc openloops: prc openloops: TBP>>=
  procedure :: write_name => prc_openloops_write_name
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_write_name (object, unit)
      class(prc_openloops_t), intent(in) :: object
      integer, intent(in), optional :: unit
    end subroutine prc_openloops_write_name
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_write_name (object, unit)
    class(prc_openloops_t), intent(in) :: object
    integer, intent(in), optional :: unit
    integer :: u
    u = given_output_unit (unit)
    write (u,"(1x,A)") "Core: OpenLoops"
  end subroutine prc_openloops_write_name

@
@ %def prc_openloops_write_name

<<Prc openloops: prc openloops: TBP>>=
  procedure :: prepare_library => prc_openloops_prepare_library
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_prepare_library (object, os_data, model)
      class(prc_openloops_t), intent(inout) :: object
      type(os_data_t), intent(in) :: os_data
      type(model_data_t), intent(in), target :: model
    end subroutine prc_openloops_prepare_library
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_prepare_library (object, os_data, model)
    class(prc_openloops_t), intent(inout) :: object
    type(os_data_t), intent(in) :: os_data
    type(model_data_t), intent(in), target :: model
    call object%load_driver (os_data)
    call object%reset_parameters ()
    call object%set_particle_properties (model)
    select type(def => object%def)
    type is (openloops_def_t)
       call object%set_verbosity (def%verbosity)
    end select
  end subroutine prc_openloops_prepare_library

@ %def prc_openloops_prepare_library
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: load_driver => prc_openloops_load_driver
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_load_driver (object, os_data)
      class(prc_openloops_t), intent(inout) :: object
      type(os_data_t), intent(in) :: os_data
    end subroutine prc_openloops_load_driver
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_load_driver (object, os_data)
    class(prc_openloops_t), intent(inout) :: object
    type(os_data_t), intent(in) :: os_data
    logical :: success
    select type (driver => object%driver)
    type is (openloops_driver_t)
       call driver%load (os_data, success)
       call driver%load_procedures (os_data, success)
    end select
  end subroutine prc_openloops_load_driver

@ %def prc_openloops_load_driver
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: start => prc_openloops_start
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_start (object)
      class(prc_openloops_t), intent(inout) :: object
    end subroutine prc_openloops_start
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_start (object)
    class(prc_openloops_t), intent(inout) :: object
    integer :: ierr
    select type (driver => object%driver)
    type is (openloops_driver_t)
       call driver%blha_olp_start (char (driver%olp_file)//c_null_char, ierr)
    end select
  end subroutine prc_openloops_start

@ %def prc_openloops_start
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: set_n_external => prc_openloops_set_n_external
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_set_n_external (object, n)
      class(prc_openloops_t), intent(inout) :: object
      integer, intent(in) :: n
    end subroutine prc_openloops_set_n_external
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_set_n_external (object, n)
    class(prc_openloops_t), intent(inout) :: object
    integer, intent(in) :: n
    N_EXTERNAL = n
  end subroutine prc_openloops_set_n_external

@ %def prc_openloops_set_n_external
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: reset_parameters => prc_openloops_reset_parameters
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_reset_parameters (object)
      class(prc_openloops_t), intent(inout) :: object
    end subroutine prc_openloops_reset_parameters
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_reset_parameters (object)
    class(prc_openloops_t), intent(inout) :: object
    integer :: ierr
    select type (driver => object%driver)
    type is (openloops_driver_t)
       call driver%blha_olp_set_parameter ('mass(5)'//c_null_char, &
            dble(openloops_default_bmass), 0._double, ierr)
       call driver%blha_olp_set_parameter ('mass(6)'//c_null_char, &
            dble(openloops_default_topmass), 0._double, ierr)
       call driver%blha_olp_set_parameter ('width(6)'//c_null_char, &
            dble(openloops_default_topwidth), 0._double, ierr)
       call driver%blha_olp_set_parameter ('mass(23)'//c_null_char, &
            dble(openloops_default_zmass), 0._double, ierr)
       call driver%blha_olp_set_parameter ('width(23)'//c_null_char, &
            dble(openloops_default_zwidth), 0._double, ierr)
       call driver%blha_olp_set_parameter ('mass(24)'//c_null_char, &
            dble(openloops_default_wmass), 0._double, ierr)
       call driver%blha_olp_set_parameter ('width(24)'//c_null_char, &
            dble(openloops_default_wwidth), 0._double, ierr)
       call driver%blha_olp_set_parameter ('mass(25)'//c_null_char, &
            dble(openloops_default_higgsmass), 0._double, ierr)
       call driver%blha_olp_set_parameter ('width(25)'//c_null_char, &
            dble(openloops_default_higgswidth), 0._double, ierr)
    end select
  end subroutine prc_openloops_reset_parameters

@ %def prc_openloops_reset_parameters
@ Set the verbosity level for openloops. The different levels are as follows:
\begin{itemize}
  \item[0] minimal output (startup message et.al.)
  \item[1] show which libraries are loaded
  \item[2] show debug information of the library loader, but not during run time
  \item[3] show debug information during run time
  \item[4] output for each call of [[set_parameters]].
\end{itemize}
<<Prc openloops: prc openloops: TBP>>=
  procedure :: set_verbosity => prc_openloops_set_verbosity
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_set_verbosity (object, verbose)
      class(prc_openloops_t), intent(inout) :: object
      integer, intent(in) :: verbose
    end subroutine prc_openloops_set_verbosity
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_set_verbosity (object, verbose)
    class(prc_openloops_t), intent(inout) :: object
    integer, intent(in) :: verbose
    integer :: ierr
    select type (driver => object%driver)
    type is (openloops_driver_t)
       call driver%blha_olp_set_parameter ('verbose'//c_null_char, &
            dble(verbose), 0._double, ierr)
    end select
  end subroutine prc_openloops_set_verbosity

@ %def prc_openloops_set_verbosity
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: prepare_external_code => &
       prc_openloops_prepare_external_code
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_prepare_external_code &
         (core, flv_states, var_list, os_data, libname, model, i_core, is_nlo)
      class(prc_openloops_t), intent(inout) :: core
      integer, intent(in), dimension(:,:), allocatable :: flv_states
      type(var_list_t), intent(in) :: var_list
      type(os_data_t), intent(in) :: os_data
      type(string_t), intent(in) :: libname
      type(model_data_t), intent(in), target :: model
      integer, intent(in) :: i_core
      logical, intent(in) :: is_nlo
    end subroutine prc_openloops_prepare_external_code
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_prepare_external_code &
       (core, flv_states, var_list, os_data, libname, model, i_core, is_nlo)
    class(prc_openloops_t), intent(inout) :: core
    integer, intent(in), dimension(:,:), allocatable :: flv_states
    type(var_list_t), intent(in) :: var_list
    type(os_data_t), intent(in) :: os_data
    type(string_t), intent(in) :: libname
    type(model_data_t), intent(in), target :: model
    integer, intent(in) :: i_core
    logical, intent(in) :: is_nlo
    integer :: ierr
    core%sqme_tree_pos = 1
    call core%set_n_external (core%data%get_n_tot ())
    call core%prepare_library (os_data, model)
    call core%start ()
    call core%set_electroweak_parameters (model)
    select type (driver => core%driver)
    type is (openloops_driver_t)
       !!! We have to set the external vector boson wavefunction to the MadGraph convention
       !!! in order to be consistent with our calculation of the spin correlated contributions.
       call driver%blha_olp_set_parameter ('wf_v_select'//c_null_char, &
            3._double, 0._double, ierr)
       if (ierr == 0) call parameter_error_message (var_str ('wf_v_select'), &
            var_str ('prc_openloops_prepare_external_code'))
    end select
    call core%read_contract_file (flv_states)
    call core%print_parameter_file (i_core)
  end subroutine prc_openloops_prepare_external_code

@ %def prc_openloops_prepare_external_code
@ Computes a spin-correlated matrix element from an interface to an
external one-loop provider. The output of [[blha_olp_eval2]] is
an array of [[dimension(16)]]. The current interface does not
give out an accuracy, so that [[bad_point]] is always [[.false.]].
OpenLoops includes a factor of 1 / [[n_hel]] in the
amplitudes, which we have to undo if polarized matrix elements
are requested.
<<Prc openloops: prc openloops: TBP>>=
  procedure :: compute_sqme_spin_c => prc_openloops_compute_sqme_spin_c
<<Prc openloops: sub interfaces>>=
    module subroutine prc_openloops_compute_sqme_spin_c (object, &
         i_flv, i_hel, em, p, ren_scale, sqme_spin_c, bad_point)
      class(prc_openloops_t), intent(inout) :: object
      integer, intent(in) :: i_flv, i_hel
      integer, intent(in) :: em
      type(vector4_t), intent(in), dimension(:) :: p
      real(default), intent(in) :: ren_scale
      real(default), intent(out), dimension(6) :: sqme_spin_c
      logical, intent(out) :: bad_point
    end subroutine prc_openloops_compute_sqme_spin_c
<<Prc openloops: procedures>>=
  module subroutine prc_openloops_compute_sqme_spin_c (object, &
       i_flv, i_hel, em, p, ren_scale, sqme_spin_c, bad_point)
    class(prc_openloops_t), intent(inout) :: object
    integer, intent(in) :: i_flv, i_hel
    integer, intent(in) :: em
    type(vector4_t), intent(in), dimension(:) :: p
    real(default), intent(in) :: ren_scale
    real(default), intent(out), dimension(6) :: sqme_spin_c
    logical, intent(out) :: bad_point
    real(default), dimension(16) :: sqme_spin_c_tmp
    real(double), dimension(5*N_EXTERNAL) :: mom
    real(double) :: res
    real(double), dimension(16) :: res_munu
    real(default) :: alpha_s
    if (object%i_spin_c(i_flv, i_hel) >= 0) then
       mom = object%create_momentum_array (p)
       if (vanishes (ren_scale)) call msg_fatal &
            ("prc_openloops_compute_sqme_spin_c: ren_scale vanishes")
       alpha_s = object%qcd%alpha%get (ren_scale)
       select type (driver => object%driver)
       type is (openloops_driver_t)
          call driver%set_alpha_s (alpha_s)
          call driver%evaluate_spin_correlations_powheg &
               (object%i_spin_c(i_flv, i_hel), mom, em, res, res_munu)
       end select
       sqme_spin_c_tmp = res_munu
       bad_point = .false.
       if (object%includes_polarization ()) &
            sqme_spin_c_tmp = object%n_hel * sqme_spin_c_tmp
       if (debug_on) then
          if (sum(sqme_spin_c_tmp) == 0) then
             call msg_debug(D_SUBTRACTION,'Spin-correlated matrix elements provided by OpenLoops are zero!')
          end if
       end if
    else
       sqme_spin_c_tmp = zero
    end if
    !!! Using symmetry of the 4x4 matrix of spin correlated squared Born MEs and
    !!! the fact that we multiply only with vectors with E=0. We thus store the
    !!! upper triangle of the lower 3x3 matrix as columns in a 1-dim array
    sqme_spin_c(1:2) = sqme_spin_c_tmp(6:7)
    sqme_spin_c(3) = sqme_spin_c_tmp(11)
    sqme_spin_c(4) = sqme_spin_c_tmp(8)
    sqme_spin_c(5) = sqme_spin_c_tmp(12)
    sqme_spin_c(6) = sqme_spin_c_tmp(16)
  end subroutine prc_openloops_compute_sqme_spin_c

@ %def prc_openloops_compute_sqme_spin_c
@
<<Prc openloops: prc openloops: TBP>>=
  procedure :: get_alpha_qed => prc_openloops_get_alpha_qed
<<Prc openloops: sub interfaces>>=
    module function prc_openloops_get_alpha_qed &
         (object, core_state) result (alpha_qed)
      class(prc_openloops_t), intent(in) :: object
      class(prc_core_state_t), intent(in), allocatable :: core_state
      real(default) :: alpha_qed
    end function prc_openloops_get_alpha_qed
<<Prc openloops: procedures>>=
  module function prc_openloops_get_alpha_qed &
       (object, core_state) result (alpha_qed)
    class(prc_openloops_t), intent(in) :: object
    class(prc_core_state_t), intent(in), allocatable :: core_state
    real(double) :: value
    real(default) :: alpha_qed
    select type (driver => object%driver)
    type is (openloops_driver_t)
       call driver%get_parameter_double ('alpha_qed'//c_null_char, value)
       alpha_qed = value
       return
    end select
    call msg_fatal ("prc_openloops_get_alpha_qed: " // &
         "called by wrong driver, only supported for OpenLoops!")
  end function prc_openloops_get_alpha_qed

@ %def prc_openloops_get_alpha_qed
